plugins {
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.8.22'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.8.22'
    id "org.openapi.generator" version "6.6.0"
}

group = 'com.robbamberg'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0")
}

tasks.compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = '17'
    }
    dependsOn("openApiGenerate")
}


tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName.set("kotlin-spring")
    inputSpec.set("$rootDir/src/main/resources/openapi/stock_api.yaml")
    apiPackage.set("com.robbamberg.intergammaassessment.openapi")
    modelPackage.set("com.robbamberg.intergammaassessment.openapi")
    configOptions = [
            interfaceOnly: "true",
            dateLibrary: "java8",
            exceptionHandler: "false",
            useTags: "true",
            useBeanValidation: "false",
            serviceInterface: "false",
    ]
    globalProperties = [
            apis: "",
            models: "",
            apiTests: "false",
    ]
}


sourceSets {
    main {
        java {
             srcDirs += "$buildDir/generate-resources/main/src"
        }
    }
}